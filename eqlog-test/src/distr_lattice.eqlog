type El;
pred le(El, El);
func meet(El, El) -> El;
func join(El, El) -> El;

// Axioms of posets.
rule {
    if x: El;
    then le(x, x);
}
rule {
    if le(x, y);
    if le(y, z);
    then le(x, z);
}
rule {
    if le(x, y);
    if le(y, x);
    then x = y;
}

// Axioms of meets.
rule {
    if x: El;
    if y: El;
    then meet(x, y)!;
}
rule {
    if meet = meet(x, y);
    then le(meet, x);
    then le(meet, y);
}
rule {
    if le(z, x);
    if le(z, y);
    if meet = meet(x, y);
    then le(z, meet);
}

// Axioms of join.
rule {
    if x: El;
    if y: El;
    then join(x, y)!;
}
rule {
    if join = join(x, y);
    then le(x, join);
    then le(y, join);
}
rule {
    if le(x, z);
    if le(y, z);
    if join = join(x, y);
    then le(join, z);
}

// Distributivity of joins over meets.
rule {
    if t_0 = x;
    if t_1 = meet(y, z);
    if to = meet(join(x, y), join(x, z));
    then join(t_0, t_1) = to;
}
rule {
    if t_0 = join(x, y);
    if t_1 = join(x, z);
    if to = join(x, meet(y, z));
    then meet(t_0, t_1) = to;
}
